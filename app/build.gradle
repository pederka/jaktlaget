apply plugin: 'com.android.application'

repositories {
    maven { url "https://maven.google.com"}
}

def apikeyPropertiesFile = rootProject.file("apikey.properties");
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    signingConfigs {
        release
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.2'
    defaultConfig {
        applicationId "net.ddns.peder.jaktlaget"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 35
        versionName "1.34"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        def addConstant = {constantName, constantValue ->
            manifestPlaceholders += [ (constantName):constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("apikey", apikeyProperties['apikey'])
        addConstant("googlemapsapikey", apikeyProperties['googlemapsapikey'])
    }
    lintOptions {
        disable 'RestrictedApi'
    }
    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
    android.signingConfigs.release.keyAlias = keystoreProperties['keyAlias']
    android.signingConfigs.release.keyPassword = keystoreProperties['keyPassword']
    android.signingConfigs.release.storeFile = file(keystoreProperties['storeFile'])
    android.signingConfigs.release.storePassword = keystoreProperties['storePassword']}
else {
    android.buildTypes.release.signingConfig = null
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-ads:19.3.0'
    testImplementation 'junit:junit:4.13'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'
    implementation 'com.jakewharton:disklrucache:2.0.2'
    implementation 'com.android.volley:volley:1.1.0'
}
